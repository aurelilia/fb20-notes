# Vorlesung am 20.10.2022
Implementierung des Interface:

```bluespec
module mkMult (HansPeter_ifc):
    // mkRegU: Uninitialized register
    Reg #(int)  w       <- mkRegU;
    Reg #(int)  x       <- mkRegU;
    Reg #(int)  y       <- mkRegU;
    // mkReg: Initialized register
    Reg #(Bool) got_x   <- mkReg (False);
    Reg #(Bool) got_y   <- mkReg (False);

    rule compute ((y != 0) && got_x && got_y);
        if (lsb(y) == 1) w <= w + x;
        x <= x << 1;
        y <= y >> 1;
    endrule

    method Action put_x (int xx) if (!got_x);
        x <= xx; w <= 0; got_x <= True;
    endmethod
    method Action put_y (int yy) if (!got_y);
        y <= yy; got_y <= True;
    endmethod

    method ActionValue #(int) get_w () if ((y == 0) && got_x && got_y);
        got_x <= False; got_y <= False;
        return w;
    endmethod
endmodule: mkMult
```

Dank Conditions/Guards werden Data Races verhindert; Methods und Rules führen
nur aus, wenn der Guard wahr ist.


## Packages
Projekte lassen sich in Packages als Dateien (`.bsv`) aufteilen.  
Zu Beginn eines Package: `package Mult;`, `endpackage: Mult` zu Ende  
Import: `import Mult::*;`

### Compile
Compileraufruf zum Compile für BSV Simulator:  
`bsc -sim -g mkTestbench -u Testbench.bsv`

`-g` gibt an, dass `mkTestbench` oberstes Modul,
`-u` dass Packages mit Dateinamen gesucht werden sollen.

Linking mit:  
`bsc -sim -e mkTestbench -o myFirstModel`

Erzeugt fertige native Executable mit Sim.


## Struktur
- Module eine rekursive Hierachie ähnlich zu Software
- Register primitive Module, Blätter der Hierache. Jedoch auch Module!
- Syntaxnotiz: Consts und Types beginnen mit Großbuchstabe, Rest klein.
  Durch Compiler enforced! `int` und `bit` Ausnahme von SV.

### Modulinstanziierung
```bluespec
interface instance_name <- module (parameters);
HansPeter_ifc m <- mkMult;
Reg#(int) w <- mkRegU;
Reg#(int) x <- mkReg (3);
```

### Methoden
- Value Methods
  - Mathematische Funktionen, Zustand der Schaltung nicht änderbar
  - Können lokale Zwischenwerte mit `let x = ...;` zuweisen
  - Haben Rückgabewert
- Action Methods
  - Zustand der Schaltung änderbar mit `<=`
  - Haben keinen Rückgabewert, Typ ist `Action`
- Action Value Methods
  - Zustand der Schaltung änderbar mit `<=`
  - Haben Rückgabewert, Typ ist `ActionValue#(type)`
